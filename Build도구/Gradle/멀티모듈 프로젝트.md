#### 멀티 모듈 프로젝트
- root settings.gradle
``` sh
rootProject.name = 'wp-pledge-backend'
include 'wp-pledge-api'
include 'wp-pledge-common'
include 'wp-pledge-proxy'
```

- root build.gradle
``` sh
plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.nbpcorp.workplace'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }
    dependencies {
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'
        implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
        implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
        implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.5.21'
        implementation group: 'io.swagger', name: 'swagger-models', version: '1.5.21'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine' }
    }
}

project(':wp-pledge-common') {
    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }
}

project(':wp-pledge-api') {
    dependencies {
        implementation project(':wp-pledge-common')
    }
}

project(':wp-pledge-proxy') {
    dependencies {
        implementation project(':wp-pledge-common')
    }
}
```
- alllprojects 블록 :  root 프로젝트와 하위 프로젝트 모두 적용되는 빌드 스크립트 기준을 작성합니다.
- subprojects 블록 : 하위 프로젝트에만 적용되는 빌드 스크립트 기준을 작성합니다.
- projects 블록 : 공통 모듈(Common)이 포함되어야 함으로 빌드시에 추가하도록 설정합니다.
-  참조용 클래스들만 있는 common 프로젝트를 위해 gradle에서는 bootRepackage.enabled 설정을 제공합니다.
```sh
//module-common 에는 실행가능한 Application을 가지고 있지 않으니 아래 내용을 포함시켜 줍니다.
bootJar { enabled = false }
jar { enabled = true }

```




### 기본 골격
- 프로젝트 루트 밑에 src 폴더가 생성되었다면 Delete
빌드는 항상 root 프로젝트를 기준으로 할 것이기 때문에 sub project의 gradle 폴더, gradlew, gradlew.bat, settings.gradle 등의 파일은 필요 없다. src 폴더와 build.gradle만 존재하도록 한다.
- 서브 프로젝트를 Gradle project로 생성하였다면 build.gradle만 존재

